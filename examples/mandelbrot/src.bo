library "mem.bo";
library "io.bo";
import "complex.bo";

const PIXEL_HEIGHT = 0.10 : f64;
const PIXEL_WIDTH = PIXEL_HEIGHT * 0.5;

#entry fn() -> u64 {
    let y = -2.0 : f64;

    // Create a stack allocated buffer
    let buffer = #uninit : [const div_to_int(4.0, PIXEL_WIDTH) - 1] u8;
    // Turn it into a 'slice'
    let buffer = &buffer : [] u8;

    while y < 2.0 {
        let x = 0.0 - 2.0 : f64;
        while x < 2.0 {
            let c = complex(x, y);
            let z = complex(0.0, 0.0);

            // This is just a showcase of the '_iters' implicit variable, you decide if it's cleaner or not.
            let i = while _iters < 1000 {
                if complex_abs(z) > 4.0 {
                    break _iters;
                };

                z = complex_add(complex_mult(z, z), c);
            } else {
                1000 : usize
            };

            *(buffer.ptr + _iters) =
                if i == 1000    { *"#".ptr }
                else if i >= 14 { *"x".ptr }
                else if i >= 10 { *".".ptr }
                else            { *" ".ptr };

            x = x + PIXEL_WIDTH;
        };

        put_string(buffer);
        put_string("\n");

        y = y + PIXEL_HEIGHT;
    };

    0
};

const div_to_int = fn(a: f64, b: f64) -> usize {
    let count = 0 : usize;
    while a > 0.0 {
        a = a - b;
        count = count + 1;
    };
    count
};
