library "mem.bo";
library "io.bo";
library "time.bo"; // start_timer, log_timer
import "complex.bo";

const PIXEL_HEIGHT = 0.10 : f64;
const PIXEL_WIDTH = 0.05 : f64;

const main = fn() -> u64 {
    let timer = start_timer();
    let y = -2.0 : f64;

    let buffer = alloc(div_to_int(4.0, PIXEL_WIDTH) - 1);

    while y < 2.0 {
        let x = 0.0 - 2.0 : f64;
        let pixel = 0 : usize;
        while x < 2.0 {
            let c = [x, y];
            let z = [0.0 : f64, 0.0];

            let i = 0 : u64;
            while (complex_abs(z) <= 4.0) && (i < 1000) {
                z = complex_add(complex_mult(z, z), c);

                i = i + 1;
            };

            *(buffer.ptr + pixel) =
                if i == 1000    { *"#".ptr }
                else if i >= 14 { *"x".ptr }
                else if i >= 10 { *".".ptr }
                else            { *" ".ptr };

            x = x + PIXEL_WIDTH;
            pixel = pixel + 1;
        };

        put_string(buffer);
        put_string("\n");

        y = y + PIXEL_HEIGHT;
    };

    log_timer(timer);

    0
};

const div_to_int = fn(a: f64, b: f64) -> usize {
    let count = 0 : usize;
    while a > 0.0 {
        a = a - b;
        count = count + 1;
    };
    count
};
