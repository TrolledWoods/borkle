library "fmt.bo";
library "io.bo";
library "mem.bo";

const true = 1 > 0;
const false = 1 < 0;

const ROM_MODE = 0;
const RAM_MODE = 1;

const string_eq = fn(a: [] u8, b: [] u8) -> bool {
    if a.len == b.len {
        for a {
            if *_it != *(b.ptr + _iteration)
                break false;
        } else true
    } else false
};

const main = fn() -> u64 {
    let mode = while true {
        print("[ROM] or [RAM] mode?\n");
        let input = read_input();
        defer dealloc(<< input);

        if string_eq(input, "ROM") break ROM_MODE;
        if string_eq(input, "RAM") break RAM_MODE;

        print("Bad input!\n");
    } else 99999;

    let use_file = while true {
        print("Enter program via file? y/n\n");
        let input = read_input();
        defer dealloc(<< input);

        if string_eq(input, "y") break true;
        if string_eq(input, "n") break false;

        print("Bad input!\n");
    } else false;

    let program_string = if use_file {
        print("Enter file name: \n");
        let path = read_input();
        read_file(path)
    } else {
        print("Enter program: \n");
        read_input()
    };

    let rom = << alloc(if mode == ROM_MODE  512 else 256) : [] u8;
    defer dealloc(<< rom);

    for rom  { *_it = 0; };

    let index = 0 : usize;
    let rom_index = 0 : usize;
    while 'outer index < program_string.len {
        let c = *(program_string.ptr + index);
        if (c == "G") ||
           (c == "O") ||
           (c == "R") ||
           (c == "B") ||
           (c == "I") ||
           (c == "T") ||
           (c == "S") ||
           (c == "A") ||
           (c == "g") ||
           (c == "o") ||
           (c == "r") ||
           (c == "b") ||
           (c == "i") ||
           (c == "t") ||
           (c == "s") ||
           (c == "a") {
            let num_total = 0 : u8;
            while (index + 1) < program_string.len {
                let num_c = *(program_string.ptr + index + 1);
                if (num_c >= "0") && (num_c <= "9") {
                    num_total = (num_total * 10) + num_c - "0";
                    index = index + 1;
                } else break;
            };

            *(rom.ptr + rom_index) = c;
            *(rom.ptr + rom_index + 1) = num_total;

            rom_index = rom_index + 2;

            if rom_index >= rom.len {
                print("WARNING! ROM/RAM overflowed\n");
                break 'outer;
            };
        };

        index = index + 1;
    };

    let ram = (
        if mode == ROM_MODE  { << alloc(256) }
        else rom
    ) : [] u8;
    defer if mode == ROM_MODE  dealloc(<< ram);

    let instr_counter = 0 : usize;
    let x = 0 : u8;
    while instr_counter < rom.len {
        let next_instr = instr_counter + 2;
        let instr = *(rom.ptr + instr_counter);
        let n = (rom.ptr + instr_counter + 1);

        if instr == "G" {
            x = *(rom.ptr + u8_to_usize(*n));
        } else if instr == "O" {
            *(rom.ptr + u8_to_usize(*n)) = x;
        } else if instr == "R" {
            let input = read_input();
            x = *input.ptr;
        } else if instr == "B" {
            if x == 0 {
                next_instr = u8_to_usize(*n) * 2;
            }
        } else if instr == "I" {
            x = x + *n;
        } else if instr == "T" {
            put_string(&[x]);
        } else if instr == "S" {
            x = *n;
        } else if instr == "A" {
            x = x + *(rom.ptr + u8_to_usize(*n));
        } else if instr == "g" {
            x = *(rom.ptr + u8_to_usize(*(rom.ptr + u8_to_usize(*n))));
        } else if instr == "o" {
            *(rom.ptr + u8_to_usize(*(rom.ptr + u8_to_usize(*n)))) = x;
        } else if instr == "r" {
            let input = read_input();
            *(rom.ptr + u8_to_usize(*n)) = *input.ptr;
        } else if instr == "b" {
            if x == 0 {
                next_instr = u8_to_usize(*(rom.ptr + u8_to_usize(*n))) * 2;
            }
        } else if instr == "i" {
            let mem = rom.ptr + u8_to_usize(*n);
            *mem = *mem + x;
        } else if instr == "t" {
            put_string(&[*(rom.ptr + u8_to_usize(*n))]);
        } else if instr == "s" {
            let a = *(rom.ptr + u8_to_usize(*n));
            let and = a & x;
            x = a - and + x;
        } else if instr == "a" {
            x = x + *(rom.ptr + u8_to_usize(*(rom.ptr + u8_to_usize(*n))));
        };

        instr_counter = next_instr;
    };
    print("\n");

    0
};

const u8_to_usize = fn(num: u8) -> usize {
    let to = 0 : usize;
    *(bit_cast &to : &u8) = num;
    to
};
