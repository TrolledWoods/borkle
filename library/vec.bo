type Vec.(T) = {
    ptr: &T;
    len: usize;
    capacity: usize;
};

const vec_new.(T) = fn() -> Vec.(T)  #0;

const INITIAL_SIZE = 16 : usize;

const as_buf.(T) = fn(vec: Vec.(T)) -> [] T {
    let buf = #uninit;
    buf.ptr = vec.ptr;
    buf.len = vec.len;
    buf
};

const unused_memory.(T) = fn(vec: &Vec.(T)) -> [] T {
    let buf = #uninit;
    buf.ptr = vec.ptr + vec.len;
    buf.len = vec.capacity - vec.len;
    buf
};

const reserve.(T) = fn(vec: &Vec.(T), requested: usize) {
    if requested > vec.capacity {
        if vec.capacity == 0 {
            let buffer = alloc(INITIAL_SIZE);
            vec.ptr = buffer.ptr;
            vec.capacity = buffer.len;
        } else {
            let old_buf = #uninit : [] T;
            old_buf.ptr = vec.ptr;
            old_buf.len = vec.capacity;

            let new_capacity = vec.capacity;
            while new_capacity <= requested {
                new_capacity = new_capacity * 2;
            };

            let new = alloc(new_capacity);
            vec.ptr = new.ptr;
            vec.capacity = new.len;

            copy_nonoverlapping(new.ptr, old_buf.ptr, vec.len);

            dealloc(old_buf);
        };
    };
};

const vec_free.(T) = fn(vec: Vec.(T)) {
    if vec.capacity > 0 {
        let buf = #uninit : [] T;
        buf.ptr = vec.ptr;
        buf.len = vec.capacity;
        dealloc(buf);
    };
};

const vec_get.(T) = fn(vec: &Vec.(T), index: usize) -> &T {
    vec.ptr + index
};

const vec_push.(T) = fn(vec: &Vec.(T), element: T) {
    vec->reserve(vec.len + 1);
    *(vec.ptr + vec.len) = element;
    vec.len = vec.len + 1;
};

const vec_push_arr.(T, #const: N) = fn(vec: &Vec.(T), buf: [N] T) {
    vec->reserve(vec.len + N);
    copy_nonoverlapping(vec.ptr + vec.len, cast &buf._0, N);
    vec.len = vec.len + N;
};

const vec_push_buf.(T) = fn(vec: &Vec.(T), buf: [] T) {
    vec->reserve(vec.len + buf.len);
    copy_nonoverlapping(vec.ptr + vec.len, buf.ptr, buf.len);
    vec.len = vec.len + buf.len;
};
