const Vec.(T) = #0 : {
    ptr: &T;
    len: usize;
    capacity: usize;
};

const vec_new.(T) = fn() -> typeof Vec.(T)  #0;

const INITIAL_SIZE = 16 : usize;

const vec_as_buffer.(T) = fn(vec: &typeof Vec.(T)) -> [] T {
    let buf = #0;
    buf.ptr = vec.ptr;
    buf.len = vec.len;
    buf
};

const vec_unused_memory.(T) = fn(vec: &typeof Vec.(T)) -> [] T {
    let buf = #0;
    buf.ptr = vec.ptr + vec.len;
    buf.len = vec.capacity - vec.len;
    buf
};

const vec_reserve.(T) = fn(vec: &typeof Vec.(T), requested: usize) {
    if requested > vec.capacity {
        if vec.capacity == 0 {
            let buffer = alloc(INITIAL_SIZE);
            vec.ptr = buffer.ptr;
            vec.capacity = buffer.len;
        } else {
            let old_buf = #uninit : [] T;
            old_buf.ptr = (*vec).ptr;
            old_buf.len = (*vec).capacity;

            let new_capacity = (*vec).capacity;
            while new_capacity < requested {
                new_capacity = new_capacity * 2;
            };

            let new = alloc(new_capacity);
            (*vec).ptr = new.ptr;
            (*vec).capacity = new.len;

            copy_nonoverlapping(new.ptr, old_buf.ptr, (*vec).len);

            dealloc(old_buf);
        };
    };
};

const vec_free.(T) = fn(vec: typeof Vec.(T)) {
    if vec.capacity > 0 {
        let buf = #uninit : [] T;
        buf.ptr = vec.ptr;
        buf.len = vec.capacity;
        dealloc(buf);
    };
};

const vec_get.(T) = fn(vec: &typeof Vec.(T), index: usize) -> &T {
    (*vec).ptr + index
};

const vec_push.(T) = fn(vec: &typeof Vec.(T), element: T) {
    vec_reserve(vec, (*vec).len + 1);
    *((*vec).ptr + (*vec).len) = element;
    (*vec).len = (*vec).len + 1;
};

const vec_push_buf.(T) = fn(vec: &typeof Vec.(T), buf: [] T) {
    vec_reserve(vec, (*vec).len + buf.len);
    copy_nonoverlapping((*vec).ptr + (*vec).len, buf.ptr, buf.len);
    (*vec).len = (*vec).len + buf.len;
};
