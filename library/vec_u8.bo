library "mem.bo";

type VecU8 {
    buf : [] u8;
    capacity : usize;

    #alias len = buf.len;
    #alias ptr = buf.ptr;
};

const INITIAL_SIZE = 16 : usize;

const vec_u8_create = fn() -> VecU8  #0;

const vec_u8_reserve = fn(vec: &VecU8, requested: usize) {
    if requested > vec.capacity {
        if vec.capacity == 0 {
            let new = alloc(INITIAL_SIZE);
            vec.ptr = << new.ptr;
            vec.capacity = new.len;
        } else {
            let old_buf = #uninit : [] u8;
            old_buf.ptr = vec.buf.ptr;
            old_buf.len = vec.capacity;

            let new_capacity = vec.capacity;
            while new_capacity < requested {
                new_capacity = new_capacity * 2;
            };

            let new = alloc(new_capacity);
            vec.ptr = << new.ptr;
            vec.capacity = new.len;
            mem_copy_nonoverlapping(<< vec.ptr, << old_buf.ptr, vec.len);

            dealloc(<< old_buf);
        };
    };
};

const vec_u8_free = fn(vec: VecU8) {
    let buf = #uninit : [] u8;
    buf.ptr = vec.ptr;
    buf.len = vec.capacity;
    dealloc(<< buf);
};

const vec_u8_get = fn(vec: &VecU8, index: usize) -> &u8 {
    vec.ptr + index
};

const vec_u8_push = fn(vec: &VecU8, character: u8) {
    vec_u8_reserve(vec, vec.len + 1);
    *(vec.ptr + vec.len) = character;
    vec.len = vec.len + 1;
};

const vec_u8_push_buf = fn(vec: &VecU8, from: [] u8) {
    vec_u8_reserve(vec, vec.len + from.len);
    mem_copy_nonoverlapping(<< (vec.ptr + vec.len), << from.ptr, from.len);
    vec.len = vec.len + from.len;
};

const vec_u8_pop = fn(vec: &VecU8) -> u8 {
    let num = *vec.ptr;
    vec.len = vec.len - 1;
    num
};

