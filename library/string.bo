library "slice.bo";
library "fmt.bo";

/// Checks if a string is empty.
const string_empty = fn(string: [] u8) -> bool  string.len == 0;

/// Checks if a byte is a whitespace character
const is_whitespace = fn(c: u8) -> bool {
    (c == " ") || (c == "\t") || (c == "\n") || (c == "\0") || (c == 0) || (c == 13)
};

/// Trims the whitespace from both the beginning and the end of a string
const string_trim = fn(string: [] u8) -> [] u8
    string_trim_end(string_trim_start(string));

/// Trims the whitespace from the beginning of a string
const string_trim_start = fn(string: [] u8) -> [] u8 {
    for string {
        if !is_whitespace(*_it) {
            break slice[u8](string, _iters, string.len);
        };
    } else &[]
};

/// Trims the whitespace from the end of a string
const string_trim_end = fn(string: [] u8) -> [] u8 {
    while _iters < string.len {
        let i = string.len - _iters - 1;
        if !is_whitespace(*(string.ptr + i))
            break slice[u8](string, 0, string.len - _iters);
    } else &[]
};

/// Returns a subsection of the given 'string', up until(not including) the first occurrance
/// of the character 'c'. It also modifies the string to not include this subsection anymore,
/// and not including the separator character either. Example;
///
/// let string = "hello\nworld";
/// consume_at(&string, "\n")
///
/// This returns "hello", and string is "world".
const string_consume_at = fn(string: &[] u8, c: u8) -> [] u8 { 'function
    if string.len == 0 {
        break 'function &[];
    };

    // Find the next new line character
    for *string {
        if *_it == c {
            let line = #uninit : [] u8;
            line.len = _iters;
            line.ptr = string.ptr;

            string.ptr = string.ptr + _iters + 1;
            string.len = string.len - _iters - 1;

            break 'function line;
        };
    };

    let new = *string;
    *string = &[];
    new
};
