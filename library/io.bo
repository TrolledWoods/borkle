library "intrinsics.bo";

const put_string = fn (buffer: [] u8) {
    i_stdout_write(buffer);
};

const put_char = fn(char: u8) {
    let temp = [char];
    i_stdout_write(&temp);
};

const debug_string = fn(buffer: [] u8) {
    for buffer {
        if _iters > 0  put_string(" ");
        print_u8(*_it);
    };
};

const print_usize = fn (num: usize)  print_u64(bit_cast num);
const print_isize = fn (num: isize)  print_i64(bit_cast num);

const get_line = fn () -> [] u8 {
    i_stdin_getline()
};

const string_to_int = fn(string: [] u8) -> u64 {
    let sum = 0 : u64;
    for string {
        let diff = *_it - "0";

        let diff_64 = 0 : u64;
        *(bit_cast &diff_64 : &u8) = diff;

        sum = (10 * sum) + diff_64;
    };

    sum
};

const print_i64 = fn(num: i64) {
    if num < 0 {
        put_char("-");
        print_u64(bit_cast -num);
    } else {
        print_u64(bit_cast num);
    };
};

const print_u64 = fn(num: u64) {
    if num == 0  put_char("0");

    let buffer = #0 : [20] u8;

    let data = #uninit : [] u8;
    data.ptr = << &buffer + 20;
    data.len = 0;

    while num > 0 {
        let next = num / 10;
        let digit = num - (next * 10);
        num = next;

        data.ptr = data.ptr - 1;
        data.len = data.len + 1;
        *data.ptr = "0" + *bit_cast &digit;
    };

    put_string(data);
};

const print_i32 = fn(num: i32) {
    let big = if num < 0 { -1 : i64 }
    else 0;
    *(bit_cast &big : &i32) = num;
    print_i64(big);
};

const print_u32 = fn(num: u32) {
    let big = 0 : u64;
    *(bit_cast &big : &u32) = num;
    print_u64(big);
};

const print_i16 = fn(num: i16) {
    let big = if num < 0 { -1 : i64 } else { 0 };
    *(bit_cast &big : &i16) = num;
    print_i64(big);
};

const print_u16 = fn(num: u16) {
    let big = 0 : u64;
    *(bit_cast &big : &u16) = num;
    print_u64(big);
};

const print_i8 = fn(num: i8) {
    let big = if num < 0 { -1 : i64 } else { 0 };
    *(bit_cast &big : &i8) = num;
    print_i64(big);
};

const print_u8 = fn(num: u8) {
    let big = 0 : u64;
    *(bit_cast &big : &u8) = num;
    print_u64(big);
};
