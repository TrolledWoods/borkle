import "day4_input.bo";
import "../main.bo";

const TestsDay4 = fn() {
    defer ok("AOC day 4");
    let results = aoc_day4();
    assert_eq(results.part_1, 46920);
    assert_eq(results.part_2, 12635);
};

const aoc_day4 = fn() -> { part_1: u64; part_2: u64; } {
    let parse = INPUT;
    take_whitespace(&parse);

    // Count the number of numbers
    let max_number = 0;
    while !empty(&parse) {
        let number = cast take_u64(&parse);
        if take_char(&parse) == s"\n"  break;
        if number > max_number  max_number = number;
    };

    let number_map = alloc.(u16)(max_number);
    defer dealloc(number_map);

    parse = INPUT;
    take_whitespace(&parse);
    while !empty(&parse) {
        let number = cast take_u64(&parse);
        if take_char(&parse) == s"\n"  break;
        *(number_map.ptr + number) = cast i;
    };

    // Count the number of boards
    take_whitespace(&parse);
    let boards_section = parse;
    let num_boards = 0;
    while !empty(&parse) {
        while i < 25 {
            skip_num(&parse);
            take_whitespace(&parse);
        };
        num_boards = num_boards + 1;
    };

    let board_cells = alloc.(u16)(num_boards * 25);
    defer dealloc(board_cells);

    // Read the numbers into the boards
    parse = boards_section;
    for cell in &board_cells {
        assert(parse.len != 0);

        let number = cast take_u64(&parse);
        *cell = number;
        take_whitespace(&parse);
    };

    // Now, go through every board
    let best_board_i = 0;
    let best_board_score = 0xffff;

    let worst_board_i = 0;
    let worst_board_score = 0;

    while i < num_boards {
        let base_i = i * 25;

        let min = 0xffff;
        // Horizontal bars
        while i < 5 {
            let x = i;
            let line_max = 0;
            while i < 5 {
                let y = i;

                let cell = board_cells.ptr + (base_i + y * 5 + x);
                let when_taken = *(number_map.ptr + cast *cell);
                if when_taken > line_max  line_max = when_taken;
            };
            if line_max < min {
                min = line_max;
            };
        };

        // Vertical bars
        while i < 5 {
            let y = i;
            let line_max = 0;
            while i < 5 {
                let x = i;

                let cell = board_cells.ptr + (base_i + y * 5 + x);
                let when_taken = *(number_map.ptr + cast *cell);
                if when_taken > line_max  line_max = when_taken;
            };
            if line_max < min {
                min = line_max;
            };
        };

        if min < best_board_score {
            best_board_i = i;
            best_board_score = min;
        };

        if min >= worst_board_score {
            worst_board_i = i;
            worst_board_score = min;
        };
    };

    let unmarked_nums = 0;
    let number_at_marked = 0;
    while i < 25 {
        let cell = board_cells.ptr + (best_board_i * 25 + i);
        let when_taken = *(number_map.ptr + cast *cell);
        if when_taken > best_board_score  unmarked_nums = unmarked_nums + cast *cell;
        if when_taken == best_board_score  number_at_marked = cast *cell;
    };

    let worst_unmarked_nums = 0;
    let worst_number_at_marked = 0;
    while i < 25 {
        let cell = board_cells.ptr + (worst_board_i * 25 + i);
        let when_taken = *(number_map.ptr + cast *cell);
        if when_taken > worst_board_score  worst_unmarked_nums = worst_unmarked_nums + cast *cell;
        if when_taken == worst_board_score  worst_number_at_marked = cast *cell;
    };

    let result = #0;
    result.part_1 = unmarked_nums * number_at_marked;
    result.part_2 = worst_unmarked_nums * worst_number_at_marked;

    result
};
